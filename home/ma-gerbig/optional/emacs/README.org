:DOC-CONFIG:
# Tangle by default to init.el, which is the most common case
#+PROPERTY: header-args:emacs-lisp :tangle init.el
#+startup: fold
:END:

#+title: Emacs Configuration
#+author: Marc-Andre Gerbig

This is my personal GNU Emacs configuration file, inspired by a bunch of resources I‚Äôve found online.
The =early-init.el= and =init.el= files are generated from this =*.org= document with =C-c C-v t=.

* Credits
| Name             | URL                                          |
|------------------+----------------------------------------------|
| Doom Emacs       | https://github.com/hlissner/doom-emacs       |
| Emmet            | https://github.com/librephoenix/nixos-config |
| Norman Walsh     | https://gitlab.com/ndw/dotfiles              |
| Protesilaos      | https://protesilaos.com/emacs/dotemacs       |
| Radon Rosborough | https://github.com/raxod502/radian           |
| SystemCrafter    | https://github.com/daviwil/dotfiles          |
| Zzamboni         | https://github.com/zzamboni/dot-doom/        |

* Early System Configuration (=early-init.el=)
:PROPERTIES:
:header-args:emacs-lisp: :tangle early-init.el
:END:
#+begin_quote
Emacs 27.1 introduced =early-init.el=, which is run before =init.el=, before package and UI initialization happens, and before site files are loaded.
#+end_quote

** File Headers
We start by simply defining the standard headers used by the two files.
These headers contain either some Emacs-LISP relevant indicators like =lexical-binding=, or instructions about the contents of the file.
#+html: <details><summary>early-init.el</summary>
#+begin_src emacs-lisp
  ;;; early-init.el -*- lexical-binding: t -*-
  ;; DO NOT EDIT THIS FILE DIRECTLY
  ;; This is a file generated from a literate programing source file located at
  ;; https://github.com/Deathlord89/nixos-config/blob/main/home/ma-gerbig/optional/emacs/README.org
  ;; You should make any changes there and regenerate it from Emacs org-mode
  ;; using org-babel-tangle (C-c C-v t)
#+end_src
#+html: </details>

#+html: <details><summary>init.el</summary>
#+begin_src emacs-lisp :tangle init.el
  ;;; early-init.el -*- lexical-binding: t -*-

  ;; DO NOT EDIT THIS FILE DIRECTLY
  ;; This is a file generated from a literate programing source file located at
  ;; https://github.com/Deathlord89/nixos-config/blob/main/home/ma-gerbig/optional/emacs/README.org
  ;; You should make any changes there and regenerate it from Emacs org-mode
  ;; using org-babel-tangle (C-c C-v t)
#+end_src
#+html: </details>

** Garbage Collection
A big contributor to startup times is garbage collection.
We up the gc threshold to temporarily prevent it from running, then reset it later by enabling =gcmh-mode=.
Not resetting it will cause stuttering/freezes.
#+begin_src emacs-lisp
  (setq gc-cons-threshold most-positive-fixnum ; 2^61 bytes
        gc-cons-percentage 0.6)
#+end_src

Calculate the maximale available memory in KB while the garbage collection is deactivated.
We use this later for the =lsp-memory-limit=. 
#+begin_src emacs-lisp
  (defvar total-memory (string-to-number (shell-command-to-string "awk '/MemTotal/ {print$2}' /proc/meminfo")))
#+end_src

** Disable package-enable-at-startup
#+begin_quote
Package initialize occurs automatically, before =user-init-file= is loaded, but after =early-init-file=. We handle package initialization, so we must prevent Emacs from doing it early!
#+end_quote

Prevent =package.el= loading packages prior to their init-file loading.
While it is technically possible to use both =package.el= and =straight.el= at the same time, there is no real reason to, and it might result in oddities like packages getting loaded more than once.
#+begin_src emacs-lisp
  (setq package-enable-at-startup nil)
#+end_src

Prevent Emacs loading outdatet packages.
#+begin_src emacs-lisp
  (setq load-prefer-newer t)
#+end_src

** Unset file-name-handler-alist
Every file opened and loaded by Emacs will run through this list to check for a proper handler for the file, but during startup, it won‚Äôt need any of them.
#+begin_src emacs-lisp
  (defvar file-name-handler-alist-original file-name-handler-alist)
  (setq file-name-handler-alist nil)
#+end_src

** Disable unnecessary interfaces.
It will be faster to disable them here before they've been initialized.
#+begin_src emacs-lisp
  (setq-default
   default-frame-alist
   '((horizontal-scroll-bars . nil)       ; No horizontal scroll-bars
     (left-fringe . 10)                   ; Thin left fringe
     (menu-bar-lines . 0)                 ; No menu bar
     (right-divider-width . 1)            ; Thin vertical window divider
     (right-fringe . 10)                  ; Thin right fringe
     (tool-bar-lines . 0)                 ; No tool bar
     (vertical-scroll-bars . nil)))       ; No vertical scroll-bars
#+end_src

** Set Cache directory
Change the user-emacs-directory to keep unwanted things out of =~/.config/emacs=.
#+BEGIN_SRC emacs-lisp
  (setq user-emacs-directory (expand-file-name "~/.cache/emacs/")
        url-history-file (expand-file-name "url/history" user-emacs-directory))
  #+END_SRC

Set the right directory to store the native comp cache.
#+BEGIN_SRC emacs-lisp
  (when (fboundp 'startup-redirect-eln-cache)
    (startup-redirect-eln-cache
     (convert-standard-filename
      (expand-file-name  "var/eln-cache/" user-emacs-directory))))
  #+END_SRC

** Old init.el file
This snippet is no longer active!
#+html: <details><summary>Old init.el</summary>
#+begin_src emacs-lisp :tangle no
  (defvar my-init-el-start-time (current-time) "Time when init.el was started")
  (setq my-user-emacs-directory "~/.emacs.d/")
  
  ;; Bootstrap straight.el
  (defvar bootstrap-version)
  (let ((bootstrap-file
         (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
        (bootstrap-version 5))
    (unless (file-exists-p bootstrap-file)
      (with-current-buffer
          (url-retrieve-synchronously
           "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
           'silent 'inhibit-cookies)
        (goto-char (point-max))
        (eval-print-last-sexp)))
    (load bootstrap-file nil 'nomessage))
  
  ;; Always use straight to install on systems other than Linux
  (setq straight-use-package-by-default t)
  
  ;; Use straight.el for use-package expressions
  (straight-use-package 'use-package)
  
  ;; Load the helper package for commands like `straight-x-clean-unused-repos'
  (require 'straight-x)
  
  (defun ma/org-mode-setup ()
    (org-indent-mode)
    (variable-pitch-mode 1)
    (auto-fill-mode 0)
    (visual-line-mode 1)
    (setq evil-auto-indent nil))
  
  (use-package org
    :hook (org-mode . ma/org-mode-setup)
    :config
    (setq org-ellipsis " ‚ñæ")
    (ma/org-font-setup))
  
  ;; =======================================================================================
  ;; The init.el file looks for "config.org" and tangles its elisp blocks (matching
  ;; the criteria described below) to "config.el" which is loaded as Emacs configuration.
  ;; Inspired and copied from: http://www.holgerschurig.de/en/emacs-init-tangle/
  ;; As of 2021-02-05, the Domain "holgerschurig.de" doesn't exist any more.
  ;; Visit archived page on https://archive.org/search.php?query=http%3A%2F%2Fwww.holgerschurig.de%2Fen%2Femacs-init-tangle%2F
  ;; =======================================================================================
  
  ;; from: http://stackoverflow.com/questions/251908/how-can-i-insert-current-date-and-time-into-a-file-using-emacs
  (defvar current-date-time-format "%a %b %d %Y-%m-%dT%H:%M:%S "
    "Format of date to insert with `insert-current-date-time' func
  See help of `format-time-string' for possible replacements")
  
  ;; from: http://stackoverflow.com/questions/251908/how-can-i-insert-current-date-and-time-into-a-file-using-emacs
  (defvar current-time-format "%a %H:%M:%S"
    "Format of date to insert with `insert-current-time' func.
  Note the weekly scope of the command's precision.")
  
  (defun my-tangle-config-org ()
    "This function will write all source blocks from =config.org= into =config.el= that are ...
  - not marked as =tangle: no=
  - doesn't have the TODO state =DISABLED=
  - have a source-code of =emacs-lisp="
    (require 'org)
    (let* ((body-list ())
           (output-file (concat my-user-emacs-directory "config.el"))
           (org-babel-default-header-args (org-babel-merge-params org-babel-default-header-args
                                                                  (list (cons :tangle output-file)))))
      (message "‚Äî‚Äî‚Äî‚Äî‚Äî‚Ä¢ Re-generating %s ‚Ä¶" output-file)
      (save-restriction
        (save-excursion
          (org-babel-map-src-blocks (concat my-user-emacs-directory "config.org")
            (let* (
                   (org_block_info (org-babel-get-src-block-info 'light))
                   ;;(block_name (nth 4 org_block_info))
                   (tfile (cdr (assq :tangle (nth 2 org_block_info))))
                   (match_for_TODO_keyword)
                   )
              (save-excursion
                (catch 'exit
                  ;;(when (string= "" block_name)
                  ;;  (message "Going to write block name: " block_name)
                  ;;  (add-to-list 'body-list (concat "message(\"" block_name "\")"));; adding a debug statement for named blocks
                  ;;  )
                  (org-back-to-heading t)
                  (when (looking-at org-outline-regexp)
                    (goto-char (1- (match-end 0))))
                  (when (looking-at (concat " +" org-todo-regexp "\\( +\\|[ \t]*$\\)"))
                    (setq match_for_TODO_keyword (match-string 1)))))
              (unless (or (string= "no" tfile)
                          (string= "DISABLED" match_for_TODO_keyword)
                          (not (string= "emacs-lisp" lang)))
                (add-to-list 'body-list (concat "\n\n;; #####################################################################################\n"
                                                "(message \"config ‚Ä¢ " (org-get-heading) " ‚Ä¶\")\n\n")
                             )
                (add-to-list 'body-list body)
                ))))
        (with-temp-file output-file
          (insert ";; ============================================================\n")
          (insert ";; Don't edit this file, edit config.org' instead ...\n")
          (insert ";; Auto-generated at " (format-time-string current-date-time-format (current-time)) " on host " system-name "\n")
          (insert ";; ============================================================\n\n")
          (insert (apply 'concat (reverse body-list))))
        (message "‚Äî‚Äî‚Äî‚Äî‚Äî‚Ä¢ Wrote %s" output-file))))
  
  
  ;; following lines are executed only when my-tangle-config-org-hook-func()
  ;; was not invoked when saving config.org which is the normal case:
  (let ((orgfile (concat my-user-emacs-directory "config.org"))
        (elfile (concat my-user-emacs-directory "config.el"))
        (gc-cons-threshold most-positive-fixnum))
    (when (or (not (file-exists-p elfile))
              (file-newer-than-file-p orgfile elfile))
      (my-tangle-config-org)
      ;;(save-buffers-kill-emacs);; TEST: kill Emacs when config has been re-generated due to many issues when loading newly generated config.el
      )
    (load-file elfile))
  
  ;; when config.org is saved, re-generate config.el:
  (defun my-tangle-config-org-hook-func ()
    (when (string= "config.org" (buffer-name))
      (let ((orgfile (concat my-user-emacs-directory "config.org"))
            (elfile (concat my-user-emacs-directory "config.el")))
        (my-tangle-config-org))))
  (add-hook 'after-save-hook 'my-tangle-config-org-hook-func)
  
  
  (message "‚Üí‚òÖ loading init.el in %.2fs" (float-time (time-subtract (current-time) my-init-el-start-time)))
#+end_src
#+html: </details>

* Basic System Configuraten (=init.el=)
Silence compiler warnings as they can be pretty disruptive
#+begin_src emacs-lisp
  (setq native-comp-async-report-warnings-errors nil)
#+end_src

** Packagemanagement with straight.el
#+begin_quote
Init-file and version lockfiles as the sole source of truth.
No persistent state kept elsewhere.
100% reproducible package management, accounting for changes in packages, recipe repositories, configuration, and the package manager itself.
#+end_quote

=straight.el= determines your package management configuration from two, and only two, sources: the contents of your init-file, and your version lockfiles.
To write the current revisions of all your packages into version lockfiles, run =M-x straight-freeze-versions=.
This will first check that =straight.el= has an up-to-date account of what packages are installed by your init-file, then ensure that all your local changes are pushed (remember, we are aiming for perfect reproducibility!).

For updading the packages run =M-x straight-pull-package= to get the latest version of a given package (or =M-x straight-pull-all= to update everything), and then =M-x straight-freeze-versions= to persist the on-disk versions to your lockfile (=~/.emacs.d/straight/versions/default.el= by default).
You can run =M-x straight-thaw-versions= at any time to reset on-disk packages to their locked versions, making your config totally reproducible across environments. 

Sometimes it's good to clean up unused repositories if I've removed packages from my configuration.  Use =straight-remove-unused-repos= for this purpose.

Let's bootstrap [[https://github.com/raxod502/straight.el][straight.el]] with:
#+begin_src emacs-lisp
(defvar bootstrap-version)
(let ((bootstrap-file
       (expand-file-name
        "straight/repos/straight.el/bootstrap.el"
        (or (bound-and-true-p straight-base-dir)
            user-emacs-directory)))
      (bootstrap-version 7))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))
#+end_src

Load the helper package for commands like `straight-x-clean-unused-repos'
TODO: =straight-x= still needed?
#+begin_src emacs-lisp
  (require 'straight-x)
#+end_src

If [[https://github.com/watchexec/watchexec][watchexec]] and [[https://www.python.org/][Python 3]] are installed, use file watchers to detect package modifications.
This saves time at startup.
Otherwise, use the ever-reliable =find(1)=.
#+begin_src emacs-lisp
  (if (and (executable-find "watchexec")
           (executable-find "python3"))
      (setq straight-check-for-modifications '(watch-files find-when-checking))
    (setq straight-check-for-modifications
          '(find-at-startup find-when-checking)))
#+end_src

*** Integration with use-package
=use-package= is a macro that provides convenient syntactic sugar for many common tasks related to installing and configuring Emacs packages.
Of course, it does not actually install the packages, but instead defers to a package manager, =like straight.el= (which comes with =use-package= integration by default).

To use =use-package=, first install it with =straight.el=:
#+begin_src emacs-lisp
  (straight-use-package 'use-package)

  (setq use-package-verbose t)
#+end_src

When configuring a feature with =use-package=, also tell =straight.el= to install a package of the same name, unless otherwise specified using the =:straight= keyword.
#+begin_src emacs-lisp
  (setq straight-use-package-by-default t)    
#+end_src

Tell =use-package= to always load features lazily unless told otherwise.
It's nicer to have this kind of thing be deterministic: if =:demand= is present, the loading is eager; otherwise, the loading is lazy.
See [[https://github.com/jwiegley/use-package#notes-about-lazy-loading][lazy loading]].
#+BEGIN_SRC emacs-lisp :tangle no
  (setq use-package-always-defer t)
#+END_SRC

Like =use-package=, but with =straight-use-package-by-default= disabled.
NAME and ARGS are as in =use-package=.
- [[https://github.com/raxod502/radian/blob/58ba58bd827e719c0eeb3d3c996d59cf4d00acd5/emacs/radian.el#L542][use-feature]] macro by [[*Credits][Radox502]]:
#+begin_src emacs-lisp
  (defmacro use-feature (name &rest args)
    (declare (indent defun))
    `(use-package ,name
       :straight nil
       ,@args))
#+end_src

**** use-package options
=use-package= has a few options to defer package loading.
This is usefull to optimise the loading time of the emacs startup.

| Option     | Package is loaded after ...                                |
|------------+------------------------------------------------------------|
| =:hook=    | the first time one of the hook is invoked                  |
| =:bind=    | the fist time one of the key bindings is used              |
| =:command= | one of the commands are used                               |
| =:mode=    | the fist time a file with a particular extension is opened |
| =:after=   | other specific packages are loaded                         |
| =:defer=   | startup                                                    |

If you want to make sure a package gets loaded at startup despite the use of any of the options above, use: =demand t=.

** Change default settings
#+BEGIN_SRC emacs-lisp
  (setq-default
   ad-redefinition-action 'accept         ; Silence warnings for redefinition
   auto-save-list-file-prefix nil         ; Prevent tracking for auto-saves
   cursor-in-non-selected-windows nil     ; Hide the cursor in inactive windows
   cursor-type '(hbar . 2)                ; Underline-shaped cursor
   custom-unlispify-menu-entries nil      ; Prefer kebab-case for titles
   custom-unlispify-tag-names nil         ; Prefer kebab-case for symbols
   delete-by-moving-to-trash t            ; Delete files to trash
   ;; fill-column 80                        ; Set width for automatic line breaks
   help-window-select t                   ; Focus new help windows when opened
   indent-tabs-mode nil                   ; Stop using tabs to indent
   inhibit-startup-screen t               ; Disable start-up screen
   initial-scratch-message ""             ; Empty the initial *scratch* buffer
   mouse-yank-at-point t                  ; Yank at point rather than pointer
   recenter-positions '(5 top bottom)     ; Set re-centering positions
   scroll-conservatively 101              ; Avoid recentering when scrolling far
   scroll-margin 2                        ; Add a margin when scrolling vertically
   select-enable-clipboard t              ; Merge system's and Emacs' clipboard
   sentence-end-double-space nil          ; Use a single space after dots
   show-help-function nil                ; Disable help text everywhere
   tab-always-indent 'complete            ; Tab indents first then tries completions
   tab-width 4                            ; Smaller width for tab characters
   uniquify-buffer-name-style 'forward    ; Uniquify buffer names
   warning-minimum-level :error           ; Skip warning buffers
   window-combination-resize t            ; Resize windows proportionally
   x-stretch-cursor t)                    ; Stretch cursor to the glyph width
  (delete-selection-mode 1)               ; Replace region when inserting text
  (fset 'yes-or-no-p 'y-or-n-p)           ; Replace yes/no prompts with y/n
  (global-subword-mode 1)                 ; Iterate through CamelCase words
  (mouse-avoidance-mode 'exile)           ; Avoid collision of mouse with point
  (put 'downcase-region 'disabled nil)    ; Enable downcase-region
  (put 'upcase-region 'disabled nil)      ; Enable upcase-region
  (set-default-coding-systems 'utf-8)     ; Default to utf-8 encoding
  (scroll-bar-mode -1)        ; Disable visible scrollbar
  (tool-bar-mode -1)          ; Disable the toolbar
  ;;(tooltip-mode -1)           ; Disable tooltips
  (set-fringe-mode 10)        ; Give some breathing room
  (menu-bar-mode -1)          ; Disable the menu bar
  (save-place-mode 1)         ; Save the last cursor position

  ;; Set up the visible bell
  (setq visible-bell t)

#+END_SRC

Global line numbering is helpful, but not useful for all modes.
#+begin_src emacs-lisp
  ;; Show line numbers
  (column-number-mode)
  (global-display-line-numbers-mode t)

  ;; Disable line numbers for some modes
  (dolist (mode'(org-mode-hook
                 term-mode-hook
                 shell-mode-hook
                 eshell-mode-hook
                 treemacs-mode-hook
                 mu4e-mode-hook))
    (add-hook mode (lambda () (display-line-numbers-mode 0))))
#+END_SRC

The =global-auto-revert-mode= will make Emacs watch the files for all open buffers for changes on disk and it will autmatically refresh those buffers if they don't have unsaved changes!
The same applys to =global-auto-revert-non-file-buffers= for directories.
#+begin_src emacs-lisp
  (global-auto-revert-mode 1)
  (setq global-auto-revert-non-file-buffers 1)
#+end_src

** Keep emacs.d clean
The default paths used to store configuration files and persistent data are not consistent across Emacs packages.
This isn't just a problem with third-party packages but even with built-in packages.

Some packages put these files directly in =user-emacs-directory= or =$HOME= or in a subdirectory of either of the two or elsewhere.
Furthermore sometimes file names are used that don't provide any insight into what package might have created them.

[[https://github.com/emacscollective/no-littering][No-littering]] sets out to fix this by changing the values of path variables to put configuration files in =no-littering-etc-directory= (defaulting to "etc/" under =user-emacs-directory=, thus usually "~/.config/emacs/etc/") and persistent data files in =no-littering-var-directory= (defaulting to "var/" under =user-emacs-directory=, thus usually "~/.emacs.d/var/"), and by using descriptive file names and subdirectories when appropriate.
This is similar to a color-theme; a "path-theme" if you will.
#+begin_src emacs-lisp
  (use-package no-littering
  :config
  ;; Theme locations where backups of various sorts are created
  (no-littering-theme-backups))
#+end_src

Disable =customize-*= routine and redirect the writing to =/dev/null=.
#+BEGIN_SRC emacs-lisp
  (setq-default custom-file null-device)
#+END_SRC

* Key Binding Configuration
** Make ESC quit prompts
#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "<escape>") 'keyboard-escape-quit)
#+END_SRC

This keybinding is overridden by evil.
To activate an alternate =universal-argument= binding enable the following codeblock.
#+BEGIN_SRC amacs-lisp :tangle no
(global-set-key (kbd "C-M-u") 'universal-argument)
#+END_SRC

** Change Leader with =general.el=
With the help of =general.el=, it is easy to manage key combinations and also serves as a wrapper for =wich-key=.
- =:ignore t= and =:which-key= bind nothing but give a description.
#+BEGIN_SRC emacs-lisp
  (use-package general
    :config
    (general-create-definer ma/leader-key-def
      :keymaps '(normal insert visual emacs)
      :prefix "SPC"
      :global-prefix "C-SPC")

    (ma/leader-key-def
      "t"  '(:ignore t :which-key "toggles")
      "tt" '(load-theme :which-key "choose theme")))
#+END_SRC

** Evil mode
- =C-z= Toggle =emacs-mode= (disable evil  keybindings)
- =C-g= Expand the default emacs "Exit" function with evils "normal" state
#+BEGIN_SRC emacs-lisp
  (defun ma/evil-hook ()
    (dolist (mode '(custom-mode
                    eshell-mode
                    git-rebase-mode
                    erc-mode
                    circe-server-mode
                    circe-chat-mode
                    circe-query-mode
                    sauron-mode
                    term-mode))
      (add-to-list 'evil-emacs-state-modes mode)))
  
  (use-package evil
    :init
    (setq evil-want-integration t)
    (setq evil-want-keybinding nil)
    (setq evil-want-C-u-scroll t)
    (setq evil-want-C-i-jump nil)
    (setq evil-respect-visual-line-mode t)
    (setq evil-undo-system 'undo-tree)
    :config
    (add-hook 'evil-mode-hook 'ma/evil-hook)
    (evil-mode 1)
    (define-key evil-insert-state-map (kbd "C-g") 'evil-normal-state)
    (define-key evil-insert-state-map (kbd "C-h") 'evil-delete-backward-char-and-join)
  
    ;; Use visual line motions even outside of visual-line-mode buffers
    (evil-global-set-key 'motion "j" 'evil-next-visual-line)
    (evil-global-set-key 'motion "k" 'evil-previous-visual-line)
  
    (evil-set-initial-state 'messages-buffer-mode 'normal)
    (evil-set-initial-state 'dashboard-mode 'normal))

  (use-package evil-collection
    :after evil
    :config
    (evil-collection-init))
#+END_SRC

** Helpful
Better help funtions with =helpful=
#+BEGIN_SRC emacs-lisp 
  (use-package helpful
    :ensure t
    :bind
    ([remap describe-function] . helpful-function)
    ([remap describe-symbol] . helpful-symbol)
    ([remap describe-variable] . helpful-variable)
    ([remap describe-command] . helpful-command)
    ([remap describe-key] . helpful-key))
#+END_SRC

Use =SPC e b= to run =eval-buffer= or =SPC e r= to run =eval-region= in the highlighted block. 
#+BEGIN_SRC emacs-lisp 
  (ma/leader-key-def
    "e"  '(:ignore t :which-key "eval")
    "eb" '(eval-buffer :which-key "eval buffer"))

  (ma/leader-key-def
    :keymaps '(visual)
    "er" '(eval-region :which-key "eval region"))
#+END_SRC

** Preserve Minibuffer History with =savehist=
#+BEGIN_SRC emacs-lisp 
  (use-package savehist
    :init
    (setq history-length 25)
    (savehist-mode 1))
#+END_SRC

** Vertico
#+html: <details><summary>Vertico Helper Funktions</summary>
Copied from Doom Emacs [[https://github.com/doomemacs/doomemacs/blob/master/modules/completion/vertico/autoload/vertico.el][Vertico]] Module.  
#+BEGIN_SRC emacs-lisp
  (defun +vertico/enter-or-preview ()
    "Enter directory or embark preview on current candidate."
    (interactive)
    (when (> 0 vertico--index)
      (user-error "No vertico session is currently active"))
    (if (and (let ((cand (vertico--candidate)))
               (or (string-suffix-p "/" cand)
                   (and (vertico--remote-p cand)
                        (string-suffix-p ":" cand))))
             (not (equal vertico--base ""))
             (eq 'file (vertico--metadata-get 'category)))
        (vertico-insert)
      (condition-case _
          (+vertico/embark-preview)
        (user-error (vertico-directory-enter)))))

  (defun +vertico/embark-preview ()
    "Previews candidate in vertico buffer, unless it's a consult command"
    (interactive)
    (unless (bound-and-true-p consult--preview-function)
      (if (fboundp 'embark-dwim)
          (save-selected-window
            (let (embark-quit-after-action)
              (embark-dwim)))
        (user-error "Embark not installed, aborting..."))))
#+END_SRC
#+html: </details>

#+BEGIN_SRC emacs-lisp 
  (use-package vertico
    :demand t
    :bind (:map vertico-map
                ("C-j" . vertico-next)
                ("C-M-j" . vertico-next-group)
                ("C-k" . vertico-previous)
                ("C-M-k" . vertico-previous-group)
                ("C-f" . vertico-exit-input)
                ("C-h" . vertico-directory-up)
                ("C-l" . +vertico/enter-or-preview)
                :map minibuffer-local-map
                ("M-h" . vertico-directory-up))
    :custom
    (vertico-resize nil)
    (vertico-count 17)
    (vertico-cycle t)

    :config
    (require 'vertico-directory)
    (vertico-mode)
    (setq-default completion-in-region-function
                  (lambda (&rest args)
                    (apply (if vertico-mode
                               #'consult-completion-in-region
                             #'completion--in-region)
                           args))))

  (use-package corfu
    :bind (:map corfu-map
                ("C-j" . corfu-next)
                ("C-k" . corfu-previous)
                ("TAB" . corfu-insert)
                ([tab] . corfu-insert)
                ("C-f" . corfu-insert))
    :custom
    (corfu-cycle t)
    (corfu-auto t)
    (corfu-preview-current nil)
    (corfu-quit-at-boundary t)
    (corfu-quit-no-match t)

    :config
    (global-corfu-mode 1))

  (use-package kind-icon
    :after corfu
    :custom (kind-icon-default-face 'corfu-default)
    :config
    (add-to-list 'corfu-margin-formatters #'kind-icon-margin-formatter))

  (use-package orderless
    :demand t
    :config
    (orderless-define-completion-style orderless+initialism
      (orderless-matching-styles '(orderless-initialism
                                   orderless-literal
                                   orderless-regexp)))

    (setq completion-styles '(orderless)
          completion-category-defaults nil
          orderless-matching-styles '(orderless-literal orderless-regexp)
          completion-category-overrides
          '((file (styles partial-completion)))))

  (use-package wgrep
    :after consult
    :hook (grep-mode . wgrep-setup))

  (use-package consult
    :demand t
    :bind (
           ([remap bookmark-jump] . consult-bookmark)
           ([remap evil-show-marks] . consult-mark)
           ([remap evil-show-registers] . consult-register)
           ([remap goto-line] . consult-goto-line)
           ([remap imenu] . consult-imenu)
           ([remap Info-search] . consult-info)
           ([remap locate] . consult-locate)
           ([remap load-theme] . consult-theme)
           ([remap man] . consult-man)
           ([remap recentf-open-files] . consult-recent-file)
           ([remap switch-to-buffer] . consult-buffer)
           ([remap switch-to-buffer-other-window] . consult-buffer-other-window)
           ([remap switch-to-buffer-other-frame] . consult-buffer-other-frame)
           ([remap yank-pop] . consult-yank-pop)
           ("C-s" . consult-line)
           ("C-M-l" . consult-imenu)
           ("C-M-j" . consult-buffer)
           ("C-x C-b" . consult-buffer)
           :map minibuffer-local-map
           ("C-r" . consult-history))
    :custom
    (completion-in-region-function #'consult-completion-in-region))

  (use-package consult-dir
    :bind (("C-x C-d" . consult-dir)
           :map vertico-map
           ("C-x C-d" . consult-dir)
           ("C-x C-j" . consult-dir-jump-file))

    :custom
    (consult-dir-project-list-function nil))

  (use-package marginalia
    :after vertico
    :custom
    (marginalia-annotators '(marginalia-annotators-heavy
                             marginalia-annotators-light
                             nil))
    :config
    (marginalia-mode))

  (use-package embark
    :after vertico
    :bind (("C-." . embark-act)
           ("M-." . embark-dwim)
           :map minibuffer-local-map
           ("C-d" . embark-act)
           :map embark-region-map
           ("D" . denote-region))

    :config
    ;; Remove the mixed indicator to prevent the popup from being displayed
    ;; automatically
    (delete #'embark-mixed-indicator embark-indicators)
    (add-to-list 'embark-indicators 'embark-minimal-indicator)

    ;; Use Embark to show command prefix help
    (setq prefix-help-command #'embark-prefix-help-command))

  (use-package embark-consult
    :after embark)

  (ma/leader-key-def
    "f"   '(:ignore t :which-key "files")
    "ff"  '(find-file :which-key "open file")
    "C-f" 'find-file
    "fr"  '(recentf :which-key "recent files")
    "fR"  '(revert-buffer :which-key "revert file")) 
#+END_SRC

** Dired
Dired is a built-in file manager for Emacs that does some pretty amazing things!

*** Key Bindings
**** Navigation
*Emacs* / *Evil*
- =n= / =j= - next line
- =p= / =k= - previous line
- =j= / =J= - jump to file in buffer
- =RET= - select file or directory
- =^= - go to parent directory
- =S-RET= / =g O= - Open file in "other" window
- =M-RET= - Show file in other window without focusing (previewing files)
- =g o= (=dired-view-file=) - Open file but in a "preview" mode, close with =q=
- =g= / =g r= Refresh the buffer with =revert-buffer= after changing configuration (and after filesystem changes!)

**** Marking files
- =m= - Marks a file
- =u= - Unmarks a file
- =U= - Unmarks all files in buffer
- =* t= / =t= - Inverts marked files in buffer
- =% m= - Mark files in buffer using regular expression
- =*= - Lots of other auto-marking functions
- =k= / =K= - "Kill" marked items (refresh buffer with =g= / =g r= to get them back)
- Many operations can be done on a single file if there are no active marks!

**** Copying and Renaming files
- =C= - Copy marked files (or if no files are marked, the current file)
- Copying single and multiple files
- =U= - Unmark all files in buffer
- =R= - Rename marked files, renaming multiple is a move!
- =% R= - Rename based on regular expression: =^test= , =old-\&=

*Power command*: =C-x C-q= (=dired-toggle-read-only=) - Makes all file names in the buffer editable directly to rename them!  Press =Z Z= to confirm renaming or =Z Q= to abort.

**** Deleting files
- =D= - Delete marked file
- =d= - Mark file for deletion
- =x= - Execute deletion for marks
- =delete-by-moving-to-trash= - Move to trash instead of deleting permanently

**** Creating and extracting archives
- =Z= - Compress or uncompress a file or folder to (=.tar.gz=)
- =c= - Compress selection to a specific file
- =dired-compress-files-alist= - Bind compression commands to file extension

**** Other common operations
- =T= - Touch (change timestamp)
- =M= - Change file mode
- =O= - Change file owner
- =G= - Change file group
- =S= - Create a symbolic link to this file
- =L= - Load an Emacs Lisp file into Emacs

* User Interface
** Dashboard
[[https://github.com/emacs-dashboard/emacs-dashboard][Emacs Dashboard]] - An extensible emacs startup screen showing you what‚Äôs most important.
#+BEGIN_SRC emacs-lisp
  (use-package dashboard
    :ensure t
    :config
    (dashboard-setup-startup-hook)
    (setq
    initial-buffer-choice (lambda () (get-buffer-create dashboard-buffer-name))
    dashboard-startup-banner 'logo
    dashboard-center-content t
    dashboard-vertically-center-content t
    dashboard-set-navigator t
    dashboard-set-init-info t
    dashboard-projects-backend 'projectile
    dashboard-items '((recents  . 5)
                      (bookmarks . 5)
                      (projects . 5)
                      (agenda . 5)
                      (registers . 5))))


    (ma/leader-key-def
      "b"   '(:ignore t :which-key "dashboard")
      "bb"  '(dashboard-refresh-buffer :which-key "open dashboard"))
#+END_SRC

** Doom Theme
[[https://github.com/hlissner/emacs-doom-themes][Doom Theme]] - A theme megapack for GNU Emacs, inspired by community favorites.
I use the custom =stylix=-enabled =mustache= theme template from [[https://github.com/librephoenix/nixos-config/blob/7a5b01ab7de1127a9ba13f88c39e4bccbc73f6ac/user/app/doom-emacs/themes/doom-stylix-theme.el.mustache][librephoenix]].
#+BEGIN_SRC emacs-lisp
  (setq custom-theme-directory "~/.config/emacs/themes")

  (use-package doom-themes
    :ensure t
    :config
    ;; Global settings (defaults)
    (setq doom-themes-enable-bold t    ; if nil, bold is universally disabled
          doom-themes-enable-italic t) ; if nil, italics is universally disabled
    (load-theme 'doom-nord t) ;; TODO: Use nix variable
    ;; Enable flashing mode-line on errors
    (doom-themes-visual-bell-config)
    ;; Corrects (and improves) org-mode's native fontification.
    (doom-themes-org-config))
    #+END_SRC

=doom-modeline= is a fancy and fast mode-line inspired by minimalism design
NOTE: The first time you load your configuration on a new machine, you'll need to run the following command interactively so that mode line icons display correctly =M-x nerd-icons-install-fonts=.
#+BEGIN_SRC emacs-lisp
  (use-package nerd-icons)

  (use-package doom-modeline
    :ensure t
    :hook (after-init . doom-modeline-mode)
    :hook (doom-modeline-mode . size-indication-mode)
    :hook (doom-modeline-mode . column-number-mode)
    :init
    (setq doom-modeline-height 25
          doom-modeline-bar-width 3
          doom-modeline-github nil
          doom-modeline-mu4e nil
          doom-modeline-persp-name nil
          doom-modeline-minor-modes nil
          doom-modeline-major-mode-icon nil
          doom-modeline-buffer-file-name-style 'relative-from-project
          doom-modeline-buffer-encoding 'nondefault)
    :config
    (defvar mouse-wheel-down-event nil)
    (defvar mouse-wheel-up-event nil))
#+END_SRC

** Treemacs
=Treemacs= - a tree layout file explorer for Emacs
#+BEGIN_SRC emacs-lisp
  (use-package treemacs
    :ensure t
    :defer t
    :config
    (progn
      (treemacs-follow-mode t)
      (treemacs-project-follow-mode t)
      (treemacs-filewatch-mode t)
      (treemacs-fringe-indicator-mode 'always)
      (when treemacs-python-executable
        (treemacs-git-commit-diff-mode t))
      (pcase (cons (not (null (executable-find "git")))
                   (not (null treemacs-python-executable)))
        (`(t . t)
         (treemacs-git-mode 'deferred))
        (`(t . _)
         (treemacs-git-mode 'simple)))

      (treemacs-hide-gitignored-files-mode nil)))

  (use-package treemacs-projectile
    :after (treemacs projectile))

  (use-package treemacs-icons-dired
    :hook (dired-mode . treemacs-icons-dired-enable-once))

  (use-package treemacs-magit
    :after (treemacs magit))
#+END_SRC

* Org-Mode
I use =use-package= to load the =org= package, and put its configuration inside the corresponding sections for keybindings (=:bind=), custom variables (=:custom=), custom faces (=:custom-face=), hooks (=:hook=) and general configuration code (=:config=), respectively.
The contents of each section is populated with the corresponding snippets that follow. See the sections below for the details on what goes into each configuration section, and some other configuration code that ends up outside this declaration.
#+begin_src emacs-lisp :noweb no-export
  (use-package org
    :bind
    (:map org-mode-map
            <<org-mode-keybindings>>)
    :custom
    <<org-mode-custom-vars>>
    ;;:custom-face
    <<org-mode-faces>>
    :hook
    <<org-mode-hooks>>
    :config
    <<org-mode-config>>)
#+end_src

** General Org Configuration
Note that mode-specific configuration variables are defined under  their corresponding packages, this  section defines only global org-mode configuration variables, which are inserted in the main =use-package= declaration for =org-mode=.
- Default directory for org files (not all are stored here).
#+begin_src emacs-lisp :tangle no  :noweb-ref org-mode-custom-vars
(org-directory-empty-p "~/org")
#+end_src

- Default org-agenda files.
  Copied from [[https://emacs.stackexchange.com/questions/74578/only-add-the-org-files-to-the-agenda-if-they-exist][StackExchange]].
#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-custom-vars
  (org-agenda-files
   (seq-filter #'file-exists-p
               (mapcar #'(lambda (file) (file-name-concat org-directory file))
                       '("Agenda.org"
                         "Inbox.org"))))
#+end_src

Workflowstates
- =TODO= - A task that should be done at same paint
- =NEXT= - This task should be done next (in the Getting Things Done sene)
- =BACK= - A task in the backlog to be done some day but not now
- =WAIT= - Waiting for someone else to be actionable again
- =DONE= - It's done!
#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-custom-vars
  (org-todo-keywords
   '((sequence "TODO(t)" "NEXT(n)" "|" "DONE(d!)")
     (sequence "|" "WAIT(w)" "BACK(b)")))
#+end_src

Automatically log done times in todo items.
#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-custom-vars
  (org-agenda-start-with-log-mode t)
  (org-log-done 'time)
  (org-log-into-drawer t)
#+end_src

Keep the indentation well structured by setting =org-startup-indented= to =t=.
This is a ust have.
Makes it feel less like editing a big text file and more like a purpose built editor for org-mode that forces the indentation.
Thanks [[https://github.com/nickanderson/Level-up-your-notes-with-Org/blob/master/Level-up-your-notes-with-Org.org#automatic-visual-indention][Nick]] for the tip!
#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-custom-vars
  (org-startup-indented t)
#+end_src

** Literate Programing
- From all  the available languages, we configure the  ones for which to load =org-babel= support.
  #+begin_src emacs-lisp :tangle no :noweb-ref org-mode-config
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((emacs-lisp . t)
       (shell . t)
       (latex . t)
       (org . t)))
  #+end_src

- This makes it so that code within =src= blocks is fontified according to their corresponding Emacs mode, making the file much more readable.
  #+begin_src emacs-lisp :tangle no :noweb-ref org-mode-custom-vars
    (org-src-fontify-natively t)
  #+end_src

- In principle this makes it so that indentation in =src= blocks works as in their native mode, but in my experience it does not always work reliably.
  For full proper indentation, always edit the code in a native buffer by pressing =C-c '=.
  #+begin_src emacs-lisp :tangle no :noweb-ref org-mode-custom-vars
    (org-src-tab-acts-natively t)
  #+end_src

- Automatically show inline images, useful when executing code that produces them, such as PlantUML or Graphviz.
  #+begin_src emacs-lisp :tangle no :noweb-ref org-mode-hooks
    (org-babel-after-execute . org-redisplay-inline-images)
  #+end_src

** Beautifying org-mode
*** Emphasis, lists and bullets
These settings make org-mode much more readable by using different fonts for headings, hiding some of the markup, etc. This was taken originally from Howard Abrams' [[http://www.howardism.org/Technical/Emacs/orgmode-wordprocessor.html][Org as a Word Processor]], and subsequently tweaked and broken up in the different parts of the =use-package= declaration by me.
- First, we set =org-hid-emphasis-markers= so that the markup indicators are not shown.
#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-custom-vars
  (org-hide-emphasis-markers t)
#+end_src

- This package makes it much easier to edit Org documents when =org-hide-emphasis-markers= is turned on. It temporarily shows the emphasis markers around certain markup elements when you place your cursor inside of them. No more fumbling around with === and =*= characters!
#+begin_src emacs-lisp
  (use-package org-appear
    :hook (org-mode . org-appear-mode))
#+end_src

- We add an entry to the org-mode font-lock table so that list markers are shown with a middle dot instead of the original character.
#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-config
  (font-lock-add-keywords
   'org-mode
   '(("^ *\\([-]\\) "
      (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "‚Ä¢"))))))
#+end_src

- Prettify checkbox lists and other symbols - courtesy of https://blog.jft.rocks/emacs/unicode-for-orgmode-checkboxes.html.
  First, we add special characters for checkboxes:
#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-hooks
  (org-mode . (lambda ()
                "Beautify Org Checkbox Symbol"
                (push '("[ ]" . "‚òê" ) prettify-symbols-alist)
                (push '("[X]" . "‚òë" ) prettify-symbols-alist)
                (push '("[-]" . "‚ä°" ) prettify-symbols-alist)
                (prettify-symbols-mode)))
#+end_src

- Show symbols when the cursor is over of right after them.
#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-custom-vars
  (prettify-symbols-unprettify-at-point 'right-edge)
#+end_src

- Second, we define a special face for checked items.
#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-config
  (defface org-checkbox-done-text
    '((t (:foreground "#71696A" :strike-through t)))
    "Face for the text part of a checked org-mode checkbox.")

  (font-lock-add-keywords
   'org-mode
   `(("^[ \t]*\\(?:[-+*]\\|[0-9]+[).]\\)[ \t]+\\(\\(?:\\[@\\(?:start:\\)?[0-9]+\\][ \t]*\\)?\\[\\(?:X\\|\\([0-9]+\\)/\\2\\)\\][^\n]*\n\\)"
      1 'org-checkbox-done-text prepend))
   'append)
#+end_src

- Show an arrow for folded headings.
#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-config
  (setq org-ellipsis " ‚ñæ")
#+end_src

*** Source code blocks
The following code ([[https://pank.eu/blog/pretty-babel-src-blocks.html][by Rasmus]]) prettifies org-mode's source blocks by replacing the =#+begin/end_src= keywords and the header arguments with symbols.
When the cursor is over or next to one of the symbols, it gets expanded into its text representation to make editing easier.
This is enabled by setting =prettify-symbols-unprettify-at-point= to ='right-edge=.
#+begin_src emacs-lisp
  (with-eval-after-load 'org
    (defvar-local rasmus/org-at-src-begin -1
      "Variable that holds whether last position was a ")

    (defvar rasmus/ob-header-symbol ?‚ò∞
      "Symbol used for babel headers")

    (defun rasmus/org-prettify-src--update ()
      (let ((case-fold-search t)
            (re "^[ \t]*#\\+begin_src[ \t]+[^ \f\t\n\r\v]+[ \t]*")
            found)
        (save-excursion
          (goto-char (point-min))
          (while (re-search-forward re nil t)
            (goto-char (match-end 0))
            (let ((args (org-trim
                         (buffer-substring-no-properties (point)
                                                         (line-end-position)))))
              (when (org-string-nw-p args)
                (let ((new-cell (cons args rasmus/ob-header-symbol)))
                  (cl-pushnew new-cell prettify-symbols-alist :test #'equal)
                  (cl-pushnew new-cell found :test #'equal)))))
          (setq prettify-symbols-alist
                (cl-set-difference prettify-symbols-alist
                                   (cl-set-difference
                                    (cl-remove-if-not
                                     (lambda (elm)
                                       (eq (cdr elm) rasmus/ob-header-symbol))
                                     prettify-symbols-alist)
                                    found :test #'equal)))
          ;; Clean up old font-lock-keywords.
          (font-lock-remove-keywords nil prettify-symbols--keywords)
          (setq prettify-symbols--keywords (prettify-symbols--make-keywords))
          (font-lock-add-keywords nil prettify-symbols--keywords)
          (while (re-search-forward re nil t)
            (font-lock-flush (line-beginning-position) (line-end-position))))))

    (defun rasmus/org-prettify-src ()
      "Hide src options via `prettify-symbols-mode'.

          `prettify-symbols-mode' is used because it has uncollpasing. It's
          may not be efficient."
      (let* ((case-fold-search t)
             (at-src-block (save-excursion
                             (beginning-of-line)
                             (looking-at "^[ \t]*#\\+begin_src[ \t]+[^ \f\t\n\r\v]+[ \t]*"))))
        ;; Test if we moved out of a block.
        (when (or (and rasmus/org-at-src-begin
                       (not at-src-block))
                  ;; File was just opened.
                  (eq rasmus/org-at-src-begin -1))
          (rasmus/org-prettify-src--update))
        ;; Remove composition if at line; doesn't work properly.
        ;; (when at-src-block
        ;;   (with-silent-modifications
        ;;     (remove-text-properties (match-end 0)
        ;;                             (1+ (line-end-position))
        ;;                             '(composition))))
        (setq rasmus/org-at-src-begin at-src-block)))

    ;; This function helps to produce a single glyph out of a
    ;; string. The glyph can then be used in prettify-symbols-alist.
    ;; This function was provided by Ihor in the org-mode mailing list.
    (defun yant/str-to-glyph (str)
      "Transform string into glyph, displayed correctly."
      (let ((composition nil))
        (dolist (char (string-to-list str)
                      (nreverse (cdr composition)))
          (push char composition)
          (push '(Br . Bl) composition))))

    (defun rasmus/org-prettify-symbols ()
      (mapc (apply-partially 'add-to-list 'prettify-symbols-alist)
            (cl-reduce 'append
                       (mapcar (lambda (x) (list x (cons (upcase (car x)) (cdr x))))
                               `(("#+begin_src" . ?‚é°) ;; ‚é° ‚û§ üñù ‚ûü ‚û§ ‚úé
                                 ;; multi-character strings can be used with something like this:
                                 ;; ("#+begin_src" . ,(yant/str-to-glyph "```"))
                                 ("#+end_src"   . ?‚é£) ;; ‚é£ ‚úê
                                 ("#+header:" . ,rasmus/ob-header-symbol)
                                 ("#+begin_quote" . ?¬´)
                                 ("#+end_quote" . ?¬ª)))))
      (turn-on-prettify-symbols-mode)
      (add-hook 'post-command-hook 'rasmus/org-prettify-src t t))
    (add-hook 'org-mode-hook #'rasmus/org-prettify-symbols))
#+end_src

* Development
** Project Management
*** Projectile
#+BEGIN_SRC emacs-lisp
  (use-package projectile
    :diminish projectile-mode
    :config
    (projectile-mode)
    (when
        (require 'magit nil t)
      (mapc #'projectile-add-known-project
            (mapcar #'file-name-as-directory
                    (magit-list-repos)))
      ;; Optionally write to persistent 'projectile-known-projects-file'
      (projectile-save-known-projects))
    :bind-keymap
    ("C-c p" . projectile-command-map))

  (ma/leader-key-def
    "p"  '(:ignore t :which-key "projectile")
    "pf"  'project-find-file
    "ps"  'projectile-switch-project
    "pF"  'consult-ripgrep
    "pb"  'consult-project-buffer
    "pc"  'projectile-compile-project
    "pd"  'projectile-dired)
#+END_SRC

Configure =magit-repository-directories= (which see) to include the desired directories.
Note that each entry can be associated with a subdirectory depth.
If you organise all your projects as subdirectories of a select few parent directories, then only the parent directories need be added to =magit-repository-directories=, with the corresponding search depth.
After Projectile is loaded, you can add all repositories reported by Magit to =projectile-known-projects=.
Foud at [[https://emacs.stackexchange.com/questions/32634/how-can-the-list-of-projects-used-by-projectile-be-manually-updated][Stackexchange]].

*** Magit
#+BEGIN_SRC emacs-lisp
  (use-package magit
    :bind ("C-M-;" . magit-status)
    :commands (magit-status magit-get-current-branch)
    :custom
    (magit-display-buffer-function #'magit-display-buffer-same-window-except-diff-v1))
    (setq magit-repository-directories
          '(;; Directory containing project root directories
            ("~/git/"      . 1)))

  (use-package magit-todos
    :after magit
    :config
    ;; (setq magit-todos-keyword-suffix "\\(?:([^)]+)\\)?:?") ; make colon optional
    (magit-todos-mode 1))

  (ma/leader-key-def
    "g"   '(:ignore t :which-key "Git")
    "gs"  '(magit-status :which-key "Git: Status")
    "gd"  '(magit-diff-unstaged :which-key "Git: Diff Unstaged")
    "gc"  '(magit-branch-or-checkout :which-key "Git: Branch / Checkout")
    "gl"  '(:ignore t :which-key "Log:")
    "glc" '(magit-log-current :which-key "Log: Current")
    "glf" '(magit-log-buffer-file :which-key "Log: Buffer File")
    "gb"  '(magit-branch :which-key "Git: Configure Branch")
    "gP"  '(magit-push-current :which-key "Git: Push")
    "gp"  '(magit-pull-branch :which-key "Git: Pull")
    "gf"  '(magit-fetch :which-key "Git: Fetch")
    "gF"  '(magit-fetch-all :which-key "Git: Fetch All")
    "gr"  '(magit-rebase :which-key "Git: Rebase"))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package git-gutter
    :hook ((prog-mode . git-gutter-mode)
           (text-mode . git-gutter-mode))
    :config
    (setq git-gutter:update-interval 0.02))

  (use-package git-gutter-fringe
    :config
    (define-fringe-bitmap 'git-gutter-fr:added [224] nil nil '(center repeated))
    (define-fringe-bitmap 'git-gutter-fr:modified [224] nil nil '(center repeated))
    (define-fringe-bitmap 'git-gutter-fr:deleted [128 192 224 240] nil nil 'bottom))
#+END_SRC

*** Direnv
It works by invoking =direnv= to obtain the environment for the current file, then updating the emacs variables =process-environment= and =exec-path=.
The result is that programs started from within emacs, such as inferior shells, linters, compilers, and test runners, will be looked up in the correct =$PATH=, and will be started with the correct environment variables set.
#+BEGIN_SRC emacs-lisp
(use-package direnv
 :config
 (direnv-mode))
#+END_SRC

** Languages
*** Language Servers
- lsp-mode
Install =lsp-mode= and set the memory limit to 80% of the available memory (described as =total-memory= in [[*Garbage Collection][Garbage Collection]]).
#+BEGIN_SRC emacs-lisp
  (use-package lsp-mode
    :commands (lsp lsp-deferred)
    :init
    (setq lsp-memory-limit (* 0.8 total-memory 1024)) ; Memory in KB convert to bytes
    (setq lsp-keymap-prefix "C-c l")
    ;; Disable features that have great potential to be slow.
    (setq lsp-enable-folding nil
          lsp-enable-text-document-color nil)
    (setq lsp-enable-on-type-formatting nil)
    ;; Make breadcrumbs opt-in; they're redundant with the modeline and imenu
    (setq lsp-headerline-breadcrumb-enable nil)
    ;; Explicitly tell lsp to use flymake; Lsp will default to flycheck if found
    ;; even if its a dependency
    (setq lsp-diagnostics-provider :flymake)
    :config
    (lsp-enable-which-key-integration t))
#+END_SRC

- lsp-ui
#+BEGIN_SRC emacs-lisp
(use-package lsp-ui
  :hook (lsp-mode . lsp-ui-mode)
  :custom
  (lsp-ui-doc-position 'bottom))
#+END_SRC

- lsp-treemacs
#+BEGIN_SRC emacs-lisp
  (use-package lsp-treemacs
    :after lsp)

  (setq lsp-treemacs-deps-position-params '((side . right)
                                            (slot . 1)
                                            (window-width . 35)))

  (setq lsp-treemacs-symbols-position-params '((side . right)
                                               (slot . 2)
                                               (window-width . 35)))
#+END_SRC

- consult-lsp
#+BEGIN_SRC emacs-lisp
(use-package consult-lsp
  :after lsp)
#+END_SRC

- company-mode
#+BEGIN_SRC emacs-lisp
  (use-package company
    :after lsp-mode
    :hook (lsp-mode . company-mode)
    :bind (:map company-active-map
           ("<tab>" . company-complete-selection))
          (:map lsp-mode-map
           ("<tab>" . company-indent-or-complete-common))
    :custom
    (company-minimum-prefix-length 1)
    (company-idle-delay 0.0))

  (use-package company-box
    :hook (company-mode . company-box-mode))
#+END_SRC

*** Nix
TODO: Set only if memory is lower than 12GB?!
#+BEGIN_SRC emacs-lisp
  (use-package nix-mode
    :mode ("\\.nix\\'" "\\.nix.in\\'")
    :hook (nix-mode . lsp-deferred)
    :config)

  (setq lsp-nix-nil-auto-eval-inputs nil)
#+END_SRC

* Features
** Parentheses
Highlights delimiters (parentheses, brackets or braces).
#+BEGIN_SRC emacs-lisp 
  (use-package rainbow-delimiters
    :hook (prog-mode . rainbow-delimiters-mode))
#+END_SRC

Enable auto close bracket insertion.
#+BEGIN_SRC emacs-lisp
  (use-package smartparens
    :bind
    ("<M-backspace>" . sp-unwrap-sexp)
    ("<M-left>" . sp-forward-barf-sexp)
    ("<M-right>" . sp-forward-slurp-sexp)
    ("<M-S-left>" . sp-backward-slurp-sexp)
    ("<M-S-right>" . sp-backward-barf-sexp)
    :hook
    (after-init . smartparens-global-mode)
    :custom
    (sp-highlight-pair-overlay t)
    (sp-highlight-wrap-overlay t)
    (sp-highlight-wrap-tag-overlay t)
    :config
    (require 'smartparens-config))
#+END_SRC

** Undotree
#+BEGIN_SRC emacs-lisp
  (use-package undo-tree
    :init
    (global-undo-tree-mode 1))
#+END_SRC

** Quality of Life
TODO: This coud be usefull.
Add visual guides towards indenting levels.
#+BEGIN_SRC emacs-lisp :tangle no
  (use-package highlight-indent-guides
    :hook
    (python-mode . highlight-indent-guides-mode)
    (scss-mode . highlight-indent-guides-mode)
    :custom
    (highlight-indent-guides-method 'character))
#+END_SRC

Colorize colors as text with their value.
#+BEGIN_SRC emacs-lisp
  (use-package rainbow-mode
    :hook
    (prog-mode . rainbow-mode)
    :custom
    (rainbow-x-colors nil))
#+END_SRC

An unobtrusive way to trim spaces from end of line.
#+BEGIN_SRC emacs-lisp
  (use-package ws-butler
    :hook ((prog-mode . ws-butler-mode)
           (text-mode . ws-butler-mode)))
#+END_SRC

[[https://github.com/justbur/emacs-which-key][Which-key]] is great for getting an overview of what keybindings are available based on the prefix keys you entered.
Learned about this one from Spacemacs.
#+BEGIN_SRC emacs-lisp
  (use-package which-key
    :init (which-key-mode)
    :diminish which-key-mode
    :config
    (setq which-key-idle-delay 0.5))
#+END_SRC

* Runtime Performance
- Garbage collection with =gcmh-mode=
#+BEGIN_QUOTE
The =emacs-startup-hook= runs later than the =after-init-hook=.
One key difference is that there may be command-line options processed after the after-init-hook and before the emacs-startup-hook.
(Command-line options are handled in multiple phases, with some being processed right away and some after init files have been loaded.)
#+END_QUOTE

Enforce a sneaky Garbage Collection strategy to minimize GC interference with the activity.
During normal use a high GC threshold is set.
When idling GC is triggered and a low threshold is set.
#+BEGIN_SRC emacs-lisp
  (use-package gcmh
    :config
    (setopt garbage-collection-messages t)
    ;; (setopt gcmh-verbose t)
    (add-hook 'emacs-startup-hook #'gcmh-mode))
#+END_SRC
